plugins {
    id 'com.android.application'
}

android {

    // Creates a variable called keystorePropertiesFile, and initializes it to the
    // keystore.properties file.
    def keystorePropertiesFile = rootProject.file("keystore.properties")

    // Initializes a new Properties() object called keystoreProperties.
    def keystoreProperties = new Properties()

    // Loads the keystore.properties file into the keystoreProperties object.
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    compileSdkVersion 30

    defaultConfig {
        applicationId "com.froggy.nsfw_gbike"
        minSdkVersion 26
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions 'appstate'

    productFlavors {
        dev {
            dimension 'appstate'
            applicationIdSuffix 'dev'
            versionCode 1
            versionName '1.0'
            targetSdkVersion 30
            minSdkVersion 26
            manifestPlaceholders = [CHANNEL:"productA"]
        }

        stable {
            dimension 'appstate'
            applicationIdSuffix 'stable'
            versionCode 1
            versionName '1.0'
            targetSdkVersion 30
            minSdkVersion 26
            manifestPlaceholders = [CHANNEL:"productB"]
        }
    }

    sourceSets {
        dev.java.srcDir 'dev'
        stable.java.srcDir 'stable'

        //dev.manifest.srcFile 'dev/AndroidManifest.xml'
        //stable.manifest.srcFile 'src/AndroidManifest.xml'
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}